sourceSets {
    itest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java {
            srcDirs("src/itest")
        }
    }
}
configurations {
    itestImplementation.extendsFrom testImplementation
    itestRuntimeOnly.extendsFrom testRuntimeOnly
}

task itest(type: Test) {
    description = 'Integration tests.'
    group = 'verification'
    useJUnitPlatform {
        excludeEngines 'junit-vintage'
    }
    testClassesDirs = sourceSets.itest.output.classesDirs
    classpath = sourceSets.itest.runtimeClasspath
    maxParallelForks Runtime.runtime.availableProcessors()

    outputs.upToDateWhen { false } // other way: inputs.upToDateWhen { false }

    shouldRunAfter test
}

check.dependsOn itest

